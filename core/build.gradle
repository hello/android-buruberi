// Used for both the 'aar' file and publish.
def VERSION_NAME = '0.9.0-SNAPSHOT'
def PACKAGE = 'is.hello.buruberi'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 23
        versionCode 1
        versionName VERSION_NAME

        testApplicationId "${PACKAGE}.tests"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'io.reactivex:rxjava:1.0.14'
    compile 'com.android.support:support-v4:23.0.1'

    testCompile "org.robolectric:robolectric:3.0"
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    repositories {
        publications {
            mavenJava(MavenPublication) {
                groupId = PACKAGE
                artifactId = 'core'
                version = VERSION_NAME

                artifact(sourceJar)
                artifact("${project.buildDir}/outputs/aar/${project.name}-release.aar")

                // The publication doesn't know about our dependencies,
                // so we have to manually add them to the pom
                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = DELEGATE_FIRST
                        name 'buruberi'
                        description 'A small library for communicating with gatt-profile Bluetooth Low Energy devices on Android.'
                        url 'https://github.com/hello/buruberi'
                        scm {
                            url 'https://github.com/hello/buruberi'
                            connection 'scm:git:git@github.com:hello/buruberi.git'
                            developerConnection 'scm:git:git@github.com:hello/buruberi.git'
                        }
                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/license/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'decarbonization'
                                name 'Kevin MacWhinnie'
                                email 'km@sayhello.com'
                            }
                        }
                    }

                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.compile.allDependencies.each {
                        // Skip the libs rule
                        if(it.group != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}
