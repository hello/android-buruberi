plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

// Used for both the 'aar' file and publish.
def VERSION_NAME = '0.9.1'
def GROUP_ID = 'is.hello'
def NAME = 'buruberi-testing'
def DESCRIPTION = 'Robolectric testing for Bluetooth Low Energy for Android'

// <http://stackoverflow.com/questions/29663918/android-gradle-javadoc-annotation-does-not-exists>
configurations {
    javadocDeps
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 23
        versionCode 1
        versionName VERSION_NAME
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-annotations:23.1.0'
    compile 'org.robolectric:robolectric:3.0'
    compile 'org.hamcrest:hamcrest-library:1.3'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps
    options.links("http://docs.oracle.com/javase/7/docs/api/");
    failOnError false
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourceJar
}

publishing {
    repositories {
        publications {
            library(MavenPublication) {
                groupId = GROUP_ID
                artifactId = NAME
                version = VERSION_NAME

                artifact(sourceJar)
                artifact(javadocJar)
                artifact("${project.buildDir}/outputs/aar/${project.name}-release.aar")

                // The publication doesn't know about our dependencies,
                // so we have to manually add them to the pom
                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = DELEGATE_FIRST
                        name NAME
                        description DESCRIPTION
                        url 'https://github.com/hello/android-buruberi'
                        scm {
                            url 'https://github.com/hello/android-buruberi'
                            connection 'scm:git:git@github.com:hello/android-buruberi.git'
                            developerConnection 'scm:git:git@github.com:hello/android-buruberi.git'
                        }
                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'decarbonization'
                                name 'Kevin MacWhinnie'
                                email 'km@sayhello.com'
                            }
                        }
                    }

                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.compile.allDependencies.each {
                        // Skip the libs rule
                        if(it.group != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

def propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    def properties = new Properties()
    properties.load(propertiesFile.newDataInputStream())
    bintray {
        user = properties.getProperty('bintrayUser')
        key = properties.getProperty('bintrayApiKey')
        publications = ['library']
        pkg {
            repo = 'maven'
            name = NAME
            userOrg = properties.getProperty('bintrayOrg')
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/hello/android-buruberi.git'
            labels = ['android', 'bluetooth', 'ble']
            publicDownloadNumbers = false
            version {
                name = VERSION_NAME
                desc = DESCRIPTION
                vcsTag = VERSION_NAME
            }
        }
    }
}

